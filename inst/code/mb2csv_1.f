      IMPLICIT REAL*8(A-H,O-Z)
	DIMENSION DOSWGT(200),WEIGHT(200),GENMIN(1),GENMAX(1),
     1  AMOUNT(200),RATE(200),GENVAL(200),SERUMC(200),COV(200,30),
     2  COVDOS(200,30),TIMDOS(200),COVMEAN(30),TIMOBS(11*200),
     3  TOTLEV(11*200,11),XLEVEL(11,200),C0(11),C1(11),C2(11),C3(11)
	INTEGER DDAY(200),DMINS(200),WDAY(200),LASTL(11),
     1  WMINS(200),TMONTH,TDAY,TYEAR,LDAY(11,200),LMINS(11,200),
     2  SDAY(200),SMINS(200),ICOVTYP(30),LASTCOV(30),ICOVDAY(200,30),
     3  ICOVMINS(200,30)
      REAL MPERTU,LASTDI,IT(200)
	CHARACTER FILFLG*20,READLINE*72,ETHDES*20
	CHARACTER*20 LSTNAM,FSTNAM
	CHARACTER*73 FILOUT,FILEIN
	CHARACTER*10 CHTNUM,WARD
      	CHARACTER*1 PATSEX
      	CHARACTER*8 TIMEU,AMTU,RATEU,LEVELU,WGTU,SCU,GENU(1)
      	CHARACTER*4 ROUTE(200),GENNAM(1)
        CHARACTER*20 COVNAM(30)
 1010 WRITE(*,1011)
 1011 FORMAT(/' THIS PROGRAM CONVERTS A PATIENT FILE IN THE OLD'/
     1' USC*PACK FORMAT INTO WORKING COPY FORMAT ... THE MEDIUM OLD'/
     2' FORMAT, BEFORE MULTIPLE DRUGS WERE ALLOWED.'//
     3' ENTER THE NAME OF A PATIENT DATA FILE IN THE OLD USC*PACK'/
     4' FORMAT: ')
      READ(*,1012) FILEIN
 1012 FORMAT(A73)
      OPEN(21,FILE=FILEIN,ERR=1020,STATUS='OLD')
      GO TO 1030
 1020 WRITE(*,1021) FILEIN
 1021 FORMAT(/' THE FOLLOWING FILE DOES NOT EXIST ... '/
     1'    ',A73)
      GO TO 1010
 1030 CLOSE(21)
 1040 WRITE(*,1031)
 1031 FORMAT(/' ENTER THE NAME OF THE NEW FILE WHICH WILL CONTAIN'/
     1' THE PATIENT INFORMATION IN THE "MEDIUM OLD" WORKING COPY '/
     2' FORMAT (I.E., BEFORE MULTIPLE DRUGS WERE ALLOWED): ')
      READ(*,1012) FILOUT
      OPEN(25,FILE=FILOUT,ERR=1050,STATUS='NEW')
      GO TO 1060
 1050 WRITE(*,1051) FILOUT
 1051 FORMAT(/' THE FOLLOWING FILE ALREADY EXISTS ...'/
     1'    ',A73)
      WRITE(*,1052)
 1052 FORMAT(/' ENTER 1 TO ENTER A NEW FILE NAME;'/
     1' ENTER 0 TO OVERWRITE THIS FILE: ')
      READ(*,*) IWRITE
      IF(IWRITE .NE. 0 .AND. IWRITE .NE. 1) GO TO 1050
      IF(IWRITE .EQ. 1) GO TO 1040
 1060 CLOSE(25)
        OPEN (1,FILE=FILEIN,Status='OLD')
        OPEN (21,FILE=FILOUT)
      IDOSEVT = 0
      READ(1,55) FILFLG
55      FORMAT (A20)
      IF (FILFLG(1:1).NE.'-') THEN
         WRITE(*,56)
56       FORMAT (/,' Your data file is an old one. New things have',
     *             ' been added.',
     *           /,' Please use Pastrx and remake the file.')
         GOTO 1220
      ENDIF
	IVER=1
	IF (FILFLG(5:5).EQ.'1') IVER=2
      READ(1,50) LSTNAM,FSTNAM,CHTNUM
50      FORMAT (2A20,A10)
	WRITE(21,49) LSTNAM,FSTNAM
   49   FORMAT(' ',' LAST AND FIRST NAMES ARE: ',A20,2X,A20)
	WRITE(21,51) CHTNUM
   51   FORMAT(' CHART NUMBER IS: ',A10)
      IF (FILFLG(2:2).NE.'1') THEN
       READ(1,75) WARD,PATAGE,PATSEX,PATHGT,DPATHT,IHUHT
75     FORMAT (A10,F5.1,A1,2F5.1,I1)
       IETHFLG=1
       ETHDES='                    '
      ENDIF
      IF(FILFLG(2:2) .EQ. '1' .AND. IVER .EQ. 1) THEN
       READ(1,80) WARD,PATAGE,PATSEX,PATHGT,DPATHT,IHUHT
80     FORMAT (A10,F10.6,A1,2F6.2,i1)
       IETHFLG=1
       ETHDES='                    '
      ENDIF
      IF(FILFLG(2:2) .EQ. '1' .AND. IVER .EQ. 2) THEN
       READ(1,85) WARD,PATAGE,PATSEX,PATHGT,DPATHT,IHUHT,IETHFLG,ETHDES
85     FORMAT (A10,F10.6,A1,2F6.2,i1,I1,A20)
      ENDIF
       	WRITE(21,76)
   76  	FORMAT(/' WARD NO, PATIENT AGE (YEARS), SEX, HEIGHT (INCHES),'/
     1' ETHNICITY FLAG, AND ETHNICITY DESCRIPTION (IF ANY) FOLLOW ON'/
     2' THE NEXT 6 LINES:')
	WRITE(21,77) WARD
   77   FORMAT(A10)
	WRITE(21,78) PATAGE
   78   FORMAT(F10.6)
	WRITE(21,79) PATSEX
   79   FORMAT(A1)
	WRITE(21,81) PATHGT
   81   FORMAT(F6.2)
	WRITE(21,82) IETHFLG
   82   FORMAT(I1)
	WRITE(21,55) ETHDES
      READ(1,100) TMONTH,TDAY,TYEAR
100      FORMAT (3I4)
	WRITE(21,83) TMONTH,TDAY,TYEAR
   83   FORMAT(/' DATE OF FIRST THERAPY IS ',3I4)
      READ(1,200) GENNAM(1),GENU(1),GENMIN(1),GENMAX(1)
200      FORMAT (A4,A8,2F7.2)
	WRITE(21,200) GENNAM(1),GENU(1),GENMIN(1),GENMAX(1)
      READ(1,300) TIMEU,AMTU,RATEU,LEVELU,WGTU,SCU,MPERTU
300      FORMAT (6A8,F7.2)
      WRITE(21,300) TIMEU,AMTU,RATEU,LEVELU,WGTU,SCU,MPERTU
      READ(1,400) LASTD
400      FORMAT (I4)
      IF (LASTD.GT.0) THEN
         DO 600 I = 1,LASTD
            IF (FILFLG(2:2).NE.'1') THEN
               READ(1,550) ROUTE(I),DDAY(I),DMINS(I),RATE(I),IT(I),
     1                      AMOUNT(I),GENVAL(I)
550	         FORMAT (A4,2I4,4F13.8)
            ELSE
               READ(1,500) ROUTE(I),DDAY(I),DMINS(I),RATE(I),IT(I),
     1                   AMOUNT(I),GENVAL(I)
500		FORMAT (A4,2I4,4F15.8)
            ENDIF
600      CONTINUE
         IF (FILFLG(2:2).NE.'1') THEN
            READ(1,700) LASTDI
         ELSE
            READ(1,750) LASTDI
         ENDIF
700         FORMAT (F7.2)
750         FORMAT (F15.8)
      ENDIF
	NUMEQT = 1
	DO IOUT=1,NUMEQT
      READ(1,*) LASTL(IOUT)
	DO I = 1,LASTL(IOUT)
	 IF(FILFLG(3:3) .EQ. ' ') READ(1,900)
     1   LDAY(IOUT,I),LMINS(IOUT,I),XLEVEL(IOUT,I)
	 IF(FILFLG(3:3) .EQ. '1') READ(1,910)
     1   LDAY(IOUT,I),LMINS(IOUT,I),XLEVEL(IOUT,I)
	END DO
900      FORMAT (2I4,F7.2)
910      FORMAT (2I4,F9.4)
	END DO
	CALL COMBINE(NUMEQT,LASTL,LDAY,LMINS,XLEVEL,LASTD,DDAY,DMINS,
     1  MPERTU,NOBTIM,TIMOBS,TOTLEV)
      READ(1,400) LASTW
      IF (LASTW.GT.0) THEN
         DO 1100 I = 1,LASTW
	IF(FILFLG(3:3) .EQ. ' ') READ(1,900) WDAY(I),WMINS(I),WEIGHT(I)
	IF(FILFLG(3:3) .EQ. '1') READ(1,910) WDAY(I),WMINS(I),WEIGHT(I)
1100     CONTINUE
      ENDIF
      READ(1,400) LASTS
      IF (LASTS.GT.0) THEN
         DO 1200 I = 1,LASTS
	IF(FILFLG(3:3) .EQ. ' ') READ(1,900) SDAY(I),SMINS(I),SERUMC(I)
	IF(FILFLG(3:3) .EQ. '1') READ(1,910) SDAY(I),SMINS(I),SERUMC(I)
1200     CONTINUE
      ENDIF
	NCOVAR = 0
	IF(IVER .EQ. 2) THEN
   60	 READ(1,57) READLINE
   57    FORMAT(A72)
	 IF(READLINE(1:1) .NE. '[') GO TO 60
	 IF(READLINE(2:6) .EQ. 'ASSAY') THEN
	  DO IEQ=1,NUMEQT
	   READ(1,*) C0(IEQ),C1(IEQ),C2(IEQ),C3(IEQ)
	  END DO
	 ENDIF
	 READ(1,57) READLINE
	 IF(READLINE(2:5) .EQ. 'USER') THEN
	 READ(1,*) NCOVAR
	 IF(NCOVAR .GT. 30) THEN
	  WRITE(*,123) FILEIN, NCOVAR
  123     FORMAT(//' PATIENT FILE '/
     1' ',A73/
     2' HAS ',I3,' USER-SUPPLIED COVARIATES. THE MAXIMUM ALLOWABLE '/
     3' NUMBER OF SUCH COVARIATES IS 30.'//
     3' PLEASE RERUN THE PROGRAM AFTER EDITING YOUR PATIENT FILES TO '/
     4' INSURE EACH HAS A MAXIMUM OF 30 USER-SUPPLIED COVARIATES. '/)
	  STOP
	 ENDIF
	 DO 1300 ICOVAR=1,NCOVAR
	 READ(1,58) COVNAM(ICOVAR),ICOVTYP(ICOVAR)
   58    FORMAT(A20,I1)
	   IF(ICOVTYP(ICOVAR) .NE. 1) THEN
	    DO I=1,3
	     READ(1,*)
	    END DO
	   ENDIF
         READ(1,400) LASTCOV(ICOVAR)
         IF (LASTCOV(ICOVAR) .GT. 0) THEN
          DO I = 1,LASTCOV(ICOVAR)
	   IF(ICOVTYP(ICOVAR) .NE. 1) READ(1,920) ICOVDAY(I,ICOVAR),
     1      ICOVMINS(I,ICOVAR),COV(I,ICOVAR)
	   IF(ICOVTYP(ICOVAR) .EQ. 1) READ(1,930) ICOVDAY(I,ICOVAR),
     1      ICOVMINS(I,ICOVAR),COV(I,ICOVAR)
	  END DO
         ENDIF
  920     FORMAT(2I4,F16.8)
  930     FORMAT(2I4,F2.0)
 1300    CONTINUE
	ENDIF
	ENDIF
      CLOSE (1)
      NUMRAT = 3 + NCOVAR
      NUMBLS = 0
	IEXT=0
	DO I=1,LASTD
	IF((ROUTE(I) .EQ. 'IM') .OR. (ROUTE(I) .EQ. 'PO') .OR.
     1  (ROUTE(I) .EQ. 'im') .OR. (ROUTE(I) .EQ. 'po')) IEXT=1
	END DO
	NUMBLS=NUMBLS+IEXT
	IF(NCOVAR .EQ. 0) WRITE(21,18)
   18   FORMAT(/' THERE ARE NO COVARIATES IN THIS DATA SET.')
	IF(NCOVAR .GE. 1) WRITE(21,22) NCOVAR
   22   FORMAT(/' THERE ARE ',I2,' COVARIATES. THEIR NAMES ARE: ')
	DO I=1,NCOVAR
	 WRITE(21,55) COVNAM(I)
	END DO
      WRITE(21,23) NUMRAT
   23 FORMAT(' ',I5,' ... NO. OF "RATES", INCLUDING WT,CCR,COVARIATES.')
      WRITE(21,24) NUMBLS
   24 FORMAT(' ',I5,' ... NO. OF BOLUS INPUT COLUMNS.')
      IF (LASTD.GT.0) THEN
         DO 2300 I = 1,LASTD
            IF ((ROUTE(I) .EQ. 'IV') .OR. (ROUTE(I) .EQ. 'iv')
     *         .OR. (ROUTE(I) .EQ. 'B') .OR. (ROUTE(I) .EQ. 'b')
     *         .OR. (ROUTE(I) .EQ. 'I') .OR. (ROUTE(I) .EQ. 'i')) THEN
                IDOSEVT = IDOSEVT + 2
            ELSE
                IDOSEVT = IDOSEVT + 1
            ENDIF
2300      CONTINUE
      ENDIF
      WRITE(21,26) IDOSEVT
   26 FORMAT(' ',I5,' ... NO. OF DOSE EVENTS. EACH DOSE EVENT HAS, IN OR
     1DER:'/
     2'           TIME, IV RATE, WT, CCR, COV VALUES (IF ANY), BOLUS VAL
     3UE.'/)
         CALL SETWGT(LASTW,LASTD,DDAY,DMINS,MPERTU,
     1  WDAY,WMINS,WEIGHT,DOSWGT)
	IF(NCOVAR .GT. 0) CALL SETCOV(NCOVAR,LASTCOV,ICOVTYP,LASTD,DDAY,
     1			  DMINS,MPERTU,ICOVDAY,ICOVMINS,COV,COVDOS)
      DO 2400 I = 1,LASTD
         TIMREG = REGTIM(DDAY(I),DMINS(I),LASTD,MPERTU,DDAY(1),DMINS(1))
	 TIMDOS(I) = TIMREG
	VALAST=AMOUNT(I)
        IF ((ROUTE(I) .EQ. 'IV') .OR. (ROUTE(I) .EQ. 'iv')
     1  .OR. (ROUTE(I) .EQ. 'B') .OR. (ROUTE(I) .EQ. 'b')
     2  .OR. (ROUTE(I) .EQ. 'I') .OR. (ROUTE(I) .EQ. 'i')) VALAST=0.0
	IF(NCOVAR .EQ. 0 .AND. NUMBLS .GT. 0) WRITE(21,*) TIMREG,
     1  RATE(I), DOSWGT(I), GENVAL(I),VALAST
	IF(NCOVAR .EQ. 0 .AND. NUMBLS .EQ. 0) WRITE(21,*) TIMREG,
     1  RATE(I), DOSWGT(I), GENVAL(I)
	IF(NCOVAR .GT. 0 .AND. NUMBLS .GT. 0) WRITE(21,*) TIMREG,
     1  RATE(I), DOSWGT(I), GENVAL(I),(COVDOS(I,J),J=1,NCOVAR),VALAST
	IF(NCOVAR .GT. 0 .AND. NUMBLS .EQ. 0) WRITE(21,*) TIMREG,
     1  RATE(I), DOSWGT(I), GENVAL(I),(COVDOS(I,J),J=1,NCOVAR)
         IF ((ROUTE(I) .EQ. 'IV') .OR. (ROUTE(I) .EQ. 'iv')
     1      .OR. (ROUTE(I) .EQ. 'B') .OR. (ROUTE(I) .EQ. 'b')
     2      .OR. (ROUTE(I) .EQ. 'I') .OR. (ROUTE(I) .EQ. 'i')) THEN
            TIMREG = TIMREG + IT(I)
            RATE(I) = 0.0
	IF(NCOVAR .EQ. 0 .AND. NUMBLS .GT. 0) WRITE(21,*) TIMREG,
     1  RATE(I), DOSWGT(I), GENVAL(I),VALAST
	IF(NCOVAR .EQ. 0 .AND. NUMBLS .EQ. 0) WRITE(21,*) TIMREG,
     1  RATE(I), DOSWGT(I), GENVAL(I)
	IF(NCOVAR .GT. 0 .AND. NUMBLS .GT. 0) WRITE(21,*) TIMREG,
     1  RATE(I), DOSWGT(I), GENVAL(I),(COVDOS(I,J),J=1,NCOVAR),VALAST
	IF(NCOVAR .GT. 0 .AND. NUMBLS .EQ. 0) WRITE(21,*) TIMREG,
     1  RATE(I), DOSWGT(I), GENVAL(I),(COVDOS(I,J),J=1,NCOVAR)
         ENDIF
2400   CONTINUE
       WRITE(21,*) NUMEQT
       WRITE(21,*) NOBTIM
        DO I = 1,NOBTIM
	 WRITE(21,*) TIMOBS(I), (TOTLEV(I,J),J=1,NUMEQT)
	END DO
	TIMMAX = TIMREG
        IF(TIMOBS(NOBTIM) .GT. TIMMAX) TIMMAX = TIMOBS(NOBTIM)
	WTMEAN = 0.0
	CCRMEAN = 0.0
	IF(NCOVAR .GT. 0) THEN
	 DO J=1,NCOVAR
	  COVMEAN(J)=0.0
	 END DO
	ENDIF
	DO IDOS = 1, LASTD-1
	 WTMEAN = WTMEAN + DOSWGT(IDOS)*(TIMDOS(IDOS+1) - TIMDOS(IDOS))
	 CCRMEAN = CCRMEAN + GENVAL(IDOS)*(TIMDOS(IDOS+1)-TIMDOS(IDOS))
	 IF(NCOVAR .GT. 0) THEN
	  DO J=1,NCOVAR
	   COVMEAN(J) = COVMEAN(J) + COVDOS(IDOS,J)*
     1                              (TIMDOS(IDOS+1)-TIMDOS(IDOS))
	  END DO
	 ENDIF
	END DO
	IF(TIMMAX .EQ. TIMDOS(1)) THEN
	 WRITE(*,17) FILEIN
   17    FORMAT(//' ************* BAD PATIENT DATA FILE *************'//
     1'  PATIENT, '/
     2' ',A73/
     3' HAS NO USEFUL INFORMATION. THE ONLY OBSERVATION TIME, IF THERE'/
     4' IS ONE, OCCURS AT THE ONLY DOSE TIME. PLEASE REMOVE THIS '/
     5' PATIENT AND RERUN THE PROGRAM. '//
     4' ************* BAD PATIENT DATA FILE *************'//)
	 STOP
	ENDIF
	WTMEAN = WTMEAN + DOSWGT(LASTD)*(TIMMAX - TIMDOS(LASTD))
	WTMEAN = WTMEAN/(TIMMAX - TIMDOS(1))
	CCRMEAN = CCRMEAN + GENVAL(LASTD)*(TIMMAX - TIMDOS(LASTD))
	CCRMEAN = CCRMEAN/(TIMMAX - TIMDOS(1))
	IF(NCOVAR .GT. 0) THEN
	  DO J=1,NCOVAR
	   COVMEAN(J) = COVMEAN(J) + COVDOS(LASTD,J)*
     1                              (TIMMAX - TIMDOS(LASTD))
	   COVMEAN(J) = COVMEAN(J)/(TIMMAX - TIMDOS(1))
	  END DO
	ENDIF
	WRITE(21,27)
   27   FORMAT(/' COVARIATE NAMES AND VALUES (1ST, LAST, AND MEAN) FOLLO
     1W:'/)
	WRITE(21,28) 'WT                  ',DOSWGT(1),DOSWGT(LASTD),
     1                WTMEAN
	WRITE(21,28) 'CCR                 ',GENVAL(1),GENVAL(LASTD),
     1                CCRMEAN
	IF(NCOVAR .GT. 0) THEN
	 DO J=1,NCOVAR
	  WRITE(21,28) COVNAM(J),COVDOS(1,J),COVDOS(LASTD,J),COVMEAN(J)
	 END DO
	ENDIF
   28   FORMAT(A20,3F15.5)
	IF(FILFLG(6:6) .EQ. '1') THEN
	 WRITE(21,*) 'ASSAY COEFFICIENTS FOLLOW:'
	  DO IEQ=1,NUMEQT
	   WRITE(21,*) C0(IEQ),C1(IEQ),C2(IEQ),C3(IEQ)
	  END DO
	ENDIF
	IF(FILFLG(6:6) .NE. '1') THEN
	 WRITE(21,*) 'ASSAY COEFFICIENTS ARE NOT INCLUDED'
	ENDIF
       CLOSE (21)
1220   STOP
       END
      	REAL*8 FUNCTION REGTIM(DAY,MINS,LASTD,MPERTU,DDAY,DMINS)
	REAL MPERTU
	INTEGER DDAY,DMINS,DAY,TOTDAY,TOTMIN
      IF (LASTD.EQ.0) THEN
         REGTIM = -1.0
      ELSE
         TOTDAY = DAY-DDAY
         TOTMIN = TOTDAY*1440
         TOTMIN = TOTMIN-DMINS
         TOTMIN = TOTMIN+MINS
         REGTIM = TOTMIN/MPERTU
      ENDIF
      RETURN
      END
	SUBROUTINE SETCOV(NCOVAR,LASTCOV,ICOVTYP,LASTD,DDAY,DMINS,
     1  MPERTU,ICOVDAY,ICOVMINS,COV,COVDOS)
      IMPLICIT REAL*8(A-H,O-Z)
	DIMENSION COVDOS(200,30),COV(200,30),TIMCOV(200,30)
      REAL MPERTU
	INTEGER DDAY(200),DMINS(200),ICOVDAY(200,30),ICOVMINS(200,30),
     1  LASTCOV(30),ICOVTYP(30)
	DO ICOV = 1,NCOVAR
	 DO I=1,LASTCOV(ICOV)
          TIMCOV(I,ICOV) = REGTIM(ICOVDAY(I,ICOV),ICOVMINS(I,ICOV),
     1			  LASTD,MPERTU,DDAY(1),DMINS(1))
	 END DO
	END DO
	DO 200 IDOS = 1,LASTD
         TIMREG = REGTIM(DDAY(IDOS),DMINS(IDOS),LASTD,MPERTU,
     1            DDAY(1),DMINS(1))
	 DO 100 ICOV=1,NCOVAR
	  IF(TIMCOV(1,ICOV) .GT. TIMREG) THEN
	   COVDOS(IDOS,ICOV) = COV(1,ICOV)
	   GO TO 100
	  ENDIF
	  IF(TIMCOV(LASTCOV(ICOV),ICOV) .LT. TIMREG) THEN
	   COVDOS(IDOS,ICOV) = COV(LASTCOV(ICOV),ICOV)
	   GO TO 100
	  ENDIF
	  DO ITIM=1,LASTCOV(ICOV)
	   IF(TIMCOV(ITIM,ICOV) .EQ. TIMREG) THEN
	    COVDOS(IDOS,ICOV) = COV(ITIM,ICOV)
	    GO TO 100
	   ENDIF
	  END DO
	  DO 50 ITIM=1,LASTCOV(ICOV)
	   IF(TIMCOV(ITIM,ICOV) .GT. TIMREG) THEN
	    IF(ICOVTYP(ICOV) .LE. 2) THEN
	     COVDOS(IDOS,ICOV)=COV(ITIM-1,ICOV)
	     GO TO 100
	    ENDIF
	    TDEL = TIMREG - TIMCOV(ITIM-1,ICOV)
	    TTOT = TIMCOV(ITIM,ICOV) - TIMCOV(ITIM-1,ICOV)
	    DELCOV = TDEL/TTOT * (COV(ITIM,ICOV) - COV(ITIM-1,ICOV))
	    COVDOS(IDOS,ICOV) = COV(ITIM-1,ICOV) + DELCOV
	    GO TO 100
	   ENDIF
   50     CONTINUE
  100    CONTINUE
  200	CONTINUE
	RETURN
	END
        SUBROUTINE SETWGT(LASTW,LASTD,DDAY,DMINS,MPERTU,
     1  WDAY,WMINS,WEIGHT,DOSWGT)
      IMPLICIT REAL*8(A-H,O-Z)
	DIMENSION DOSWGT(200),WEIGHT(200),TIMWGT(200)
      REAL MPERTU
	INTEGER DDAY(200),DMINS(200),WDAY(200),WMINS(200)
	 DO I=1,LASTW
          TIMWGT(I) = REGTIM(WDAY(I),WMINS(I),LASTD,MPERTU,DDAY(1),
     1		      DMINS(1))
	 END DO
	DO 200 IDOS = 1,LASTD
         TIMREG = REGTIM(DDAY(IDOS),DMINS(IDOS),LASTD,MPERTU,
     1            DDAY(1),DMINS(1))
	  IF(TIMWGT(1) .GT. TIMREG) THEN
	   DOSWGT(IDOS) = WEIGHT(1)
	   GO TO 100
	  ENDIF
	  IF(TIMWGT(LASTW) .LT. TIMREG) THEN
	   DOSWGT(IDOS) = WEIGHT(LASTW)
	   GO TO 100
	  ENDIF
	  DO ITIM=1,LASTW
	   IF(TIMWGT(ITIM) .EQ. TIMREG) THEN
	    DOSWGT(IDOS) = WEIGHT(ITIM)
	    GO TO 100
	   ENDIF
	  END DO
	  DO ITIM=1,LASTW
	   IF(TIMWGT(ITIM) .GT. TIMREG) THEN
	    TDEL = TIMREG - TIMWGT(ITIM-1)
	    TTOT = TIMWGT(ITIM) - TIMWGT(ITIM-1)
	    DELWGT = TDEL/TTOT * (WEIGHT(ITIM) - WEIGHT(ITIM-1))
	    DOSWGT(IDOS) = WEIGHT(ITIM-1) + DELWGT
	    GO TO 100
	   ENDIF
	  END DO
  100    CONTINUE
  200	CONTINUE
	RETURN
	END
	SUBROUTINE COMBINE(NUMEQT,LASTL,LDAY,LMINS,XLEVEL,LASTD,DDAY,
     1  DMINS,MPERTU,NOBTIM,TIMOBS,TOTLEV)
      IMPLICIT REAL*8(A-H,O-Z)
	DIMENSION TIMOBS(11*200),TOTLEV(11*200,11),TIM(11,200),
     1  XLEVEL(11,200)
      REAL MPERTU
	INTEGER LASTL(11),LDAY(11,200),LMINS(11,200),DDAY(200),
     1  DMINS(200)
	ITIM = 0
	DO IOUT = 1,NUMEQT
         DO J = 1,LASTL(IOUT)
          TIM(IOUT,J) = REGTIM(LDAY(IOUT,J),LMINS(IOUT,J),LASTD,MPERTU,
     1    DDAY(1),DMINS(1))
	  ITIM = ITIM+1
	  TIMOBS(ITIM) = TIM(IOUT,J)
	 END DO
	END DO
	CALL ORDTIM(ITIM,TIMOBS,NOBTIM)
	DO ITIM = 1,NOBTIM
	 DO IOUT=1,NUMEQT
	  DO J=1,LASTL(IOUT)
	   IF(TIM(IOUT,J) .EQ. TIMOBS(ITIM)) THEN
	    TOTLEV(ITIM,IOUT) = XLEVEL(IOUT,J)
	    GO TO 10
	   ENDIF
	  END DO
	  TOTLEV(ITIM,IOUT) = -99.
   10	 END DO
	END DO
	RETURN
	END
	SUBROUTINE ORDTIM(N,TIMOBS,NOBTIM)
      IMPLICIT REAL*8(A-H,O-Z)
	DIMENSION TIMOBS(11*200),TIMTEMP(11*200)
	CURMAX = -1.0
	DO I=1,N
	 IF(TIMOBS(I) .GT. CURMAX) THEN
	  CURMAX = TIMOBS(I)
	 ENDIF
	END DO
	NOBTIM = 0
	PREVMIN = -1.0
   10	CURMIN = 1.D30
	DO I=1,N
	 T = TIMOBS(I)
	 IF(T .GT. PREVMIN .AND. T .LT. CURMIN) THEN
	  CURMIN = T
	 ENDIF
	END DO
	NOBTIM = NOBTIM+1
	TIMTEMP(NOBTIM) = CURMIN
	PREVMIN = CURMIN
	IF(CURMIN .LT. CURMAX) GO TO 10
	DO I=1,NOBTIM
	 TIMOBS(I) = TIMTEMP(I)
	END DO
	RETURN
	END
